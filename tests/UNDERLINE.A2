CALL-151

1C00:8D 52 C0 8D 54 C0 A9 5B CD 51 C0 D0 FB EA CD 51
1C10:C0 D0 F5 A9 28 20 A8 1D A9 5B A0 17 A2 08 D0 02
1C20:A2 07 CD 51 C0 D0 FB CA D0 F8 8D 55 C0 CD 50 C0
1C30:D0 FB 8D 54 C0 8D 51 C0 88 D0 E5 F0 CB

1DA8: 38
1DA9: 48
1DAA: E9 01
1DAC: D0 FC
1DAE: 68
1DAF: E9 01
1DB1: D0 F6
1DB3: 60

E000G

  1 REM
  2 REM
  3 REM  CALL UNDERLINE
  4 REM
  5 REM
  6 REM  FIRST MOVE INTEGER LOW POINTERS UP TO $1E40.
  7 REM
  8 REM
  9 IF PEEK(-8192) = 32 THEN 20
 10 PRINT "THIS PROGRAM REQUIRES INTEGER BASIC."
 15 GOTO 200
 20 POKE 74, 64 : POKE 75, 30 : POKE 204, 64 : POKE 205, 30
 22 POKE -16304, 0 : POKE -16299, 0 : POKE -16298, 0 : REM LOOK AT LORES PAGE 2.
 25 REM
 26 REM
 27 REM  NOW FILL PAGE 2 WITH THE UNDERLINES FOR THE CURRENT TEXT DISPLAY.
 28 REM
 29 REM
 30 FOR P2 = 2048 TO 2176 : POKE P2, 0 : NEXT P2 : REM BLANK TOP LINE OF PAGE 2
 40 FOR P1 = 1024 TO 1919 : P2 = P1+1152 : GOSUB 160
 45 NEXT P1
 50 FOR P1 = 1920 TO 1999 : P2 = P1+168 : GOSUB 160
 55 NEXT P1
 60 REM
 61 REM
 62 REM  NOW FLAG HBL SCANNED MEMORY WITH "$5B"S.
 63 REM
 65 REM
 70 FOR A = 0 TO 1920 STEP 128 : FOR B = 0 TO 23
 75 FLAG = 0 : IF B < 6 THEN FLAG = 91 : REM  91 IS FLASHING LEFT BRACKET
 80 POKE 5136+A+B, FLAG : REM HBL BEFORE MIDDLE SCREEN = FLAG
 90 POKE 5176+A+B, FLAG : REM HBL BEFORE BOTTOM SCREEN = FLAG
100 NEXT B
110 FOR B = 0 TO 31
115 FLAG = 0 : IF B < 14 THEN FLAG = 91 : REM  91 IS FLASHING LEFT BRACKET
120 POKE 5216+A+B, FLAG : REM HBL BEFORE VBL AND TOP SCREEN = FLAG
130 NEXT B : NEXT A
140 CALL 7168 : REM GO SPLIT SCREEN
150 GOTO 200
155 REM
156 REM COLOR = 0 OR 1 OR 15
157 REM
160 T = PEEK(P1) : COLR = 1 : REM  UNDERLINE COLOR = 1
165 IF T = 32 THEN COLR = 15 : REM  INVERSE SPACE?
170 IF T = 96 OR T = 160 OR T = 224 THEN COLR = 0 : REM SPACE?
180 POKE P2, COLR : RETURN
200 END

RUN
