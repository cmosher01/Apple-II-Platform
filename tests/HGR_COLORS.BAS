 10 PRINT  CHR$ (4)"PR#3"
 20 TEXT : HOME : VTAB 20: HGR 
 30 REM  ****** COLOR BANDS PURPLE BLUE GREEN ORANGE (ORDERED BY HALF DOT
 50 HCOLOR = 2: HPLOT 0,10 TO 279,10
 60 HCOLOR = 6: HPLOT 0,11 TO 279,11
 70 HCOLOR = 1: HPLOT 0,12 TO 279,12
 80 HCOLOR = 5: HPLOT 0,13 TO 279,13
 90 HCOLOR = 3: HPLOT 0,14 TO 279,14
100 HCOLOR = 7: HPLOT 0,15 TO 279,15
110 GET A$
120 HGR 
130 C(0) = 2:C(1) = 6:C(2) = 1:C(3) = 5
140 REM  ****** DRAW 2 COLORS SIDE BY SIDE.
150 REM         LEFT COLOR ON EVEN BYTE, RIGHT ON ODD.
160 XL(0) = 6:XL(1) = 6:XL(2) = 5:XL(3) = 5
170 XR(0) = 8:XR(1) = 8:XR(2) = 7:XR(3) = 7
180 FOR CL = 0 TO 3: FOR CR = 0 TO 3:X = (CL * 4 + CR) * 14
190 HCOLOR = C(CL): HPLOT X + XL(CL),0 TO X + XL(CL),79
210 HCOLOR = C(CR): HPLOT X + XR(CR),0 TO X + XR(CR),79
230 NEXT : NEXT 
240 REM  ****** DRAW 2 COLORS SIDE BY SIDE.
250 REM         LEFT COLOR ON ODD BYTE, RIGHT ON EVEN.
260 XL(0) = 12:XL(1) = 12:XL(2) = 13:XL(3) = 13
270 XR(0) = 14:XR(1) = 14:XR(2) = 15:XR(3) = 15
280 FOR CL = 0 TO 3: FOR CR = 0 TO 3:X = (CL * 4 + CR) * 14
290 HCOLOR = C(CL): HPLOT X + XL(CL),80 TO X + XL(CL),159
310 HCOLOR = C(CR): HPLOT X + XR(CR),80 TO X + XR(CR),159
330 NEXT : NEXT 
340 REM  ****** DRAW DARK BLUE, BROWN, LIGHT BLUE, YELLOW LINES.
350 HCOLOR = 7: HPLOT 230,0 TO 230,79: HPLOT 237,80 TO 237,159
360 HCOLOR = 3: HPLOT 244,0 TO 244,79: HPLOT 251,80 TO 251,159
380 HCOLOR = 4: HPLOT 245,0 TO 245,79: HPLOT 252,80 TO 252,159
400 REM  ****** LABEL THE COLOR LINES WITH THE COLORS USED ON THE LEFT AND
410 REM         RIGHT SIDE OF THE BYTE BOUNDARY. ALSO LABEL OTHER COLORS.
420 C$(0) = "P":C$(1) = "B":C$(2) = "G":C$(3) = "O"
440 FOR CL = 0 TO 3: FOR CR = 0 TO 3: PRINT "  "C$(CL)C$(CR);: NEXT : NEXT 
450 PRINT "  BcOcPsGs"
460 PRINT "TOP ROW: EVEN BYTE/ODD BYTE             ";
470 PRINT "BOTTOM ROW: ODD BYTE/EVEN BYTE"
480 REM  ****** CLEAR AND SET HIGH BITS OF SOME ROWS OF EVEN/ODD COLOR LINES.
490 FOR I = 0 TO 279 STEP 14
500 HCOLOR = 0: HPLOT I,30 TO I,39: HPLOT I + 13,30 TO I + 13,39
510 HCOLOR = 4: HPLOT I,40 TO I,49: HPLOT I + 13,40 TO I + 13,49: NEXT 
520 REM  ****** CLEAR AND SET HIGH BITS OF SOME ROWS OF ODD/EVEN COLOR LINES.
530 FOR I = 7 TO 266 STEP 14
540 HCOLOR = 0: HPLOT I,110 TO I,119: HPLOT I + 13,110 TO I + 13,119
550 HCOLOR = 4: HPLOT I,120 TO I,129: HPLOT I + 13,120 TO I + 13,129: NEXT 
